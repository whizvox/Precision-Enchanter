plugins {
  id 'net.minecraftforge.gradle' version '5.1.+'
  id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

group = 'me.whizvox.precisionenchanter'
version = '1.19.2-dev-20230317'

java {
  archivesBaseName = 'precisionenchanter'
  toolchain.languageVersion = JavaLanguageVersion.of(17)
}

minecraft {
  mappings channel: 'parchment', version: '2022.11.27-1.19.2'
  runs {
    client {
      workingDirectory project.file('run')
      property 'forge.logging.markers', 'REGISTRIES'
      property 'forge.logging.console.level', 'debug'
      property 'forge.enabledGameTestNamespaces', 'precisionenchanter'

      mods {
        precisionenchanter {
          source sourceSets.main
        }
      }
    }

    server {
      workingDirectory project.file('run')
      property 'forge.logging.markers', 'REGISTRIES'
      property 'forge.logging.console.level', 'debug'
      property 'forge.enabledGameTestNamespaces', 'precisionenchanter'
      mods {
        precisionenchanter {
          source sourceSets.main
        }
      }
    }

    gameTestServer {
      workingDirectory project.file('run')
      property 'forge.logging.markers', 'REGISTRIES'
      property 'forge.logging.console.level', 'debug'
      property 'forge.enabledGameTestNamespaces', 'precisionenchanter'
      mods {
        precisionenchanter {
          source sourceSets.main
        }
      }
    }

    data {
      workingDirectory project.file('run')
      property 'forge.logging.markers', 'REGISTRIES'
      property 'forge.logging.console.level', 'debug'
      args '--mod', 'precisionenchanter', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
      mods {
        precisionenchanter {
          source sourceSets.main
        }
      }
    }
  }
}

// Include resources generated by data generators
sourceSets.main.resources {
  // Include all files
  include '**/**'
  // ...but exclude dev-only files, such as Blockbench model files, Photoshop files, and the .cache from data
  // generation
  exclude '**/*.bbmodel'
  exclude '**/*.psd'
  exclude '.cache'
  srcDir 'src/generated/resources'
}

repositories {
  maven {
    url = "https://modmaven.dev/"
  }
  maven {
    // location of the maven that hosts JEI files
    name = "Progwml6 maven"
    url = "https://dvs1.progwml6.com/files/maven/"
  }
  maven {
    name = "Jared's maven"
    url = "https://maven.blamejared.com/"
  }
  maven {
    name 'CurseMaven'
    url "https://www.cursemaven.com"
    content {
      includeGroup "curse.maven"
    }
  }
}

sourceSets {
  checkVersion {
    java {
      srcDirs('src/checkversion')
    }
  }
}

dependencies {
  minecraft 'net.minecraftforge:forge:1.19.2-43.2.0'

  implementation fg.deobf('com.teamcofh:cofh_core:1.19.2-10.2.0.38')
  // for some reason the ensorcellation artifact isn't updated on modmaven, so we have to pull from curse maven instead
  implementation fg.deobf('curse.maven:ensorcellation-349447:4382362')

  runtimeOnly fg.deobf('mezz.jei:jei-1.19.2-forge:11.6.0.1011')

  checkVersionImplementation 'commons-cli:commons-cli:1.5.0'
}

jar {
  manifest {
    attributes([
        "Specification-Title"     : "precisionenchanter",
        "Specification-Vendor"    : "whizvox",
        "Specification-Version"   : "1",
        "Implementation-Title"    : project.name,
        "Implementation-Version"  : project.jar.archiveVersion,
        "Implementation-Vendor"   : "whizvox",
        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    ])
  }
}

jar.finalizedBy('reobfJar')

jar {
  // don't include data generation package in final JAR file
  exclude 'me/whizvox/precisionenchanter/data/**'
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

tasks.register('checkRemoteVersion', JavaExec) {
  group = 'other'
  description = 'Compare remote project version with local'
  classpath = sourceSets.checkVersion.runtimeClasspath
  mainClass.set('me.whizvox.precisionenchanting.CheckRemoteVersion')
}
