plugins {
  id 'net.minecraftforge.gradle' version '5.1.+'
  id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

group = 'me.whizvox.precisionenchanter'
version = '1.19.2-1.0.0'

java {
  archivesBaseName = 'precisionenchanter'
  toolchain.languageVersion = JavaLanguageVersion.of(17)
}

minecraft {
  mappings channel: 'parchment', version: '2022.11.27-1.19.2'
  runs {
    client {
      workingDirectory project.file('run')
      property 'forge.logging.markers', 'REGISTRIES'
      property 'forge.logging.console.level', 'debug'
      property 'forge.enabledGameTestNamespaces', 'precisionenchanter'
      // apparently these 2 lines are required for some mod mixins to work
      property 'mixin.env.remapRefMap', 'true'
      property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
      mods {
        precisionenchanter {
          source sourceSets.main
        }
      }
    }

    server {
      workingDirectory project.file('runserver')
      property 'forge.logging.markers', 'REGISTRIES'
      property 'forge.logging.console.level', 'debug'
      property 'forge.enabledGameTestNamespaces', 'precisionenchanter'
      property 'mixin.env.remapRefMap', 'true'
      property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
      mods {
        precisionenchanter {
          source sourceSets.main
        }
      }
    }

    gameTestServer {
      workingDirectory project.file('run')
      property 'forge.logging.markers', 'REGISTRIES'
      property 'forge.logging.console.level', 'debug'
      property 'forge.enabledGameTestNamespaces', 'precisionenchanter'
      property 'mixin.env.remapRefMap', 'true'
      property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
      mods {
        precisionenchanter {
          source sourceSets.main
        }
      }
    }

    data {
      workingDirectory project.file('run')
      property 'forge.logging.markers', 'REGISTRIES'
      property 'forge.logging.console.level', 'debug'
      args '--mod', 'precisionenchanter', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
      property 'mixin.env.remapRefMap', 'true'
      property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
      mods {
        precisionenchanter {
          source sourceSets.main
        }
      }
    }
  }
}

// Include resources generated by data generators
sourceSets.main.resources {
  // Include all files
  include '**/**'
  // ...but exclude dev-only files, such as Blockbench model files, Photoshop files, and the .cache from data
  // generation
  exclude '**/*.bbmodel'
  exclude '**/*.psd'
  exclude '.cache'
  srcDir 'src/generated/resources'
}

repositories {
  // CoFH Core, Twilight Forest
  maven {
    url = "https://modmaven.dev/"
  }
  // JEI
  maven {
    name = "Progwml6 maven"
    url = "https://dvs1.progwml6.com/files/maven/"
  }
  // Patchouli
  maven {
    name = "Jared's maven"
    url = "https://maven.blamejared.com/"
  }
  // everything else
  maven {
    name 'CurseMaven'
    url "https://www.cursemaven.com"
    content {
      includeGroup "curse.maven"
    }
  }
}

dependencies {
  minecraft 'net.minecraftforge:forge:1.19.2-43.2.6'

  implementation fg.deobf('com.teamcofh:cofh_core:1.19.2-10.2.0.38')
  implementation fg.deobf('curse.maven:ensorcellation-349447:4382362') // on modmaven, but not updated to 1.19.2
  implementation fg.deobf('curse.maven:placebo-283644:4441950') // apotheosis library mod
  implementation fg.deobf('curse.maven:apotheosis-313970:4443341')
  implementation fg.deobf('curse.maven:vanillatweaks-272637:4013671')
  implementation fg.deobf('teamtwilight:twilightforest:4.2.1518:universal')

  runtimeOnly fg.deobf('mezz.jei:jei-1.19.2-forge:11.6.0.1011')
  runtimeOnly fg.deobf('vazkii.patchouli:Patchouli:1.19.2-77')
}

jar {
  manifest {
    attributes([
        "Specification-Title"     : "precisionenchanter",
        "Specification-Vendor"    : "whizvox",
        "Specification-Version"   : "1",
        "Implementation-Title"    : project.name,
        "Implementation-Version"  : project.jar.archiveVersion,
        "Implementation-Vendor"   : "whizvox",
        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    ])
  }
}

jar.finalizedBy('reobfJar')

jar {
  // don't include data generation package in final JAR file
  exclude 'me/whizvox/precisionenchanter/data/**'
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}
